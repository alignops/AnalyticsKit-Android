/*
 * Copyright 2016 - 2022 busybusy, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

plugins {
	id 'com.android.library'
	id 'kotlin-android'
}

android {
	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	defaultConfig {
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
	}
	buildTypes {
		release {
			debuggable false
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			debuggable true
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	sourceSets {
		test.java.srcDirs += 'src/test/kotlin'
	}
}

dependencies {
	api "androidx.annotation:annotation:1.2.0"

	testImplementation 'junit:junit:4.12'
	testImplementation "org.assertj:assertj-core:$rootProject.ext.assertjVersion"
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.ext.kotlinVersion"
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$rootProject.ext.kotlinVersion"
}

afterEvaluate {
	publishing {
		publications {
			releaseAnalyticsKit(MavenPublication) { // Creates a Maven publication called "releaseAnalyticsKit".
				from components.release // Applies the component for the release build variant.

				groupId = 'com.github.busybusy.AnalyticsKit-Android'
				artifactId = 'analyticskit'
				version = "$VERSION_NAME"
			}
			debugAnalyticsKit(MavenPublication) { // Creates a Maven publication called “debugAnalyticsKit”.
				from components.debug // Applies the component for the debug build variant.

				groupId = 'com.github.busybusy.AnalyticsKit-Android'
				artifactId = 'analyticskit-debug'
				version = "$VERSION_NAME"
			}
		}
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
	failOnError false
	source = android.sourceSets.main.java.sourceFiles
	setClasspath(getClasspath() + project.files(android.getBootClasspath().join(File.pathSeparator)))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier.set("javadoc")
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
