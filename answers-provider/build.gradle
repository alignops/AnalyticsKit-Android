/*
 * Copyright 2018 Busy, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'io.fabric' //Put Fabric plugin after Android plugin

android {
	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	defaultConfig {
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		versionCode 2
		versionName "0.2.0"
	}
	buildTypes {
		release {
			debuggable false
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			debuggable true
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}

crashlytics { // needed due to 'Crashlytics couln't find the manifest' on AGP 3.3.2+
	manifestPath = "$buildDir/intermediates/library_manifest/debug/AndroidManifest.xml"
}

dependencies {
	implementation project(path: rootProject.ext.analytics_kit)
	implementation "com.android.support:support-annotations:$rootProject.ext.supportAnnotationsVersion"
	implementation('com.crashlytics.sdk.android:answers:1.4.7@aar') { // The actual Answers SDK
		transitive = true
	}

	testImplementation 'com.crashlytics.sdk.android:answers:1.4.7@aar'
	testImplementation 'junit:junit:4.12'
	testImplementation "org.assertj:assertj-core:$rootProject.ext.assertjVersion"
	testImplementation 'org.robolectric:robolectric:3.5.1'
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
	failOnError  false
	source = android.sourceSets.main.java.sourceFiles
	setClasspath(getClasspath() + project.files(android.getBootClasspath().join(File.pathSeparator)))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier.set("javadoc")
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
