/*
 * Copyright 2018 busybusy, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	defaultConfig {
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		versionCode 1
		versionName "0.1.0"
	}
	buildTypes {
		release {
			debuggable false
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			debuggable true
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	testOptions {
		unitTests.returnDefaultValues = true
	}
}

dependencies {
	implementation project(path: rootProject.ext.analytics_kit)
	implementation "com.android.support:support-annotations:$rootProject.ext.supportAnnotationsVersion"
	compileOnly 'io.intercom.android:intercom-sdk:5.5.1'

	testImplementation 'io.intercom.android:intercom-sdk:5.5.1'
	testImplementation 'junit:junit:4.12'
	testImplementation "org.assertj:assertj-core:$rootProject.ext.assertjVersion"
	//noinspection GradleDependency Held back from the project's version due to incompatibility between Powermock and Mockito2
	testImplementation "org.mockito:mockito-core:1.10.19" // TODO 2019-12-19: figure out how to make Powermock and Mockito2 play nicely together

	testImplementation "org.powermock:powermock-api-mockito:1.6.6"
	//noinspection GradleDependency Held back from the latest version due to incompatibility between Powermock and Mockito2
	testImplementation "org.powermock:powermock-module-junit4:1.6.6" // TODO 2019-12-19: figure out how to make Powermock and Mockito2 play nicely together
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
	failOnError false
	source = android.sourceSets.main.java.sourceFiles
	setClasspath(getClasspath() + project.files(android.getBootClasspath().join(File.pathSeparator)))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier.set("javadoc")
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
